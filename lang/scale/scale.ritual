extern int_to_rune(i:int):rune
extern runes_to_string(runes:[]rune):string
extern string_to_int(text:string, base:int):int

struct GetName {
    loc:location
    name:Token
}

struct GetAttr {
    loc:location
    expr:Expr
    name:Token
}

struct Call {
    loc:location
    expr:Expr
    args:[]Expr
}

struct TupleLiteral {
    loc:location
    args:[]Expr
}

struct IntLiteral {
    loc:location
    text:string
    base:int
}

struct StringLiteral {
    loc:location
    value:string
}

struct Assign {
    loc:location
    target:Expr
    value:Expr
}

struct Let {
    loc:location
    expr:Expr
}

struct Sequence {
    children:[]Expr
}

struct While {
    loc:location
    cond:Expr
    body:Expr
}

struct BinaryOp {
    loc:location
    left:Expr
    op:string
    right:Expr
}

union Expr = GetName | Call | GetAttr | TupleLiteral | IntLiteral | StringLiteral | Assign | Let | Sequence | While | BinaryOp;

struct NamedTypeRef {
    name:Token
}

struct ListTypeRef {
    t:TypeRef
}

union TypeRef = NamedTypeRef | ListTypeRef;


struct Param {
    name:Token
    t:TypeRef
}

struct FuncDecl {
    name:Token
    params:[]Param
    returns:[]TypeRef
    body:Expr
}

struct FieldDecl {
    name:Token
    t:TypeRef
}

struct StructDecl {
    name:Token
    fields:[]FieldDecl
}

struct ImportDecl {
    loc:location
    path:[]string
}

struct ExternFuncDecl {
    name:Token
    params:[]Param
    returns:[]TypeRef
}

union Decl = FuncDecl | StructDecl | ImportDecl | ExternFuncDecl;

struct Module {
    name:string
    path:string
    decls:[]Decl
}

func S():void {
    /([ \t\n\r]|"//"[^\n]*)*/
}

//func s():void { /[ \t]*/ }
//func EOS():void { /S ";" | s !!"}" | S [^]/ }

func hex_digit():rune {
    /[0-9a-fA-Z]/
}

func int_literal():IntLiteral {
    /l=loc text=<[0-9]+>/; IntLiteral{l, text, 10}
}

func escape_char():rune {
    /[\\]/;
    ( /[n]/; int_to_rune(0x0A)
    | /[r]/; int_to_rune(0x0D)
    | /[t]/; int_to_rune(0x09)
    | /[0]/; int_to_rune(0x00)
    | /[\\]/; int_to_rune(0x5C)
    | /[x]/; int_to_rune(string_to_int(</hex_digit hex_digit/>, 16))
    | /[u][{] digits=<hex_digit+> [}]/; int_to_rune(string_to_int(digits, 16))
    )
}

func string_value():string {
    /["]/;
    c = []rune{};
    (
        c << /escape_char | [\\]["] | [^\\"]/
    )*;
    /["]/;
    runes_to_string(c)
}

func string_literal():StringLiteral {
    StringLiteral{loc(), string_value()}
}

func expr_atom():Expr {
    (/l=loc "(" S/; 
        (e=expr();
            (args=[]Expr{e};
                /(S "," S args<<expr)+/;
                /S ")"/;
                TupleLiteral{l, args}
            | e
            )
        | /")"/; TupleLiteral{l, []Expr{}}
        )
    | int_literal()
    | string_literal()
    | /l=loc "let" end_of_keyword S/; Let{l, expr_postfix()}
    | /l=loc "while" end_of_keyword S cond=expr S "{" S body=expr S "}"/; While{l, cond, body}
    | GetName{loc(), ident()}
    )
}

func expr_postfix():Expr {
    e=expr_atom();
    ( /S l=loc "." S/; e=GetAttr{l, e, ident()}
    | /S l=loc "(" S/; args=[]Expr{}; /(args<<expr (S "," S args<<expr)*)? S ")"/; e=Call{l, e, args}
    )*;
    e
}

func expr_add():Expr {
    e=expr_postfix();
    (
        /S l=loc op=<"+"|"-"> S other=expr_postfix/;
        e = BinaryOp{l, e, op, other}
    )*;
    e
}

func expr_compare():Expr {
    e=expr_add();
    (
        /S l=loc op=<"<="|"<"|">="|">"|"!="|"=="> S other=expr_add/;
        e = BinaryOp{l, e, op, other}
    )?;
    e
}

func expr_assign():Expr {
    e=expr_compare();
    (
        /S l=loc "=" S/;
        value=expr_compare();
        e=Assign{l, e, value}
    )?;
    e
}

func expr_sequence():Expr {
    e=expr_assign();
    (
        exprs=[]Expr{e};
        (
            /S ";" S/;
            exprs<<expr_assign()
        )+;
        e=Sequence{exprs}
    )?;
    e
}

func expr():Expr {
    expr_sequence()
}

func type_ref():TypeRef {
    NamedTypeRef{ident()}
    | /"[]" S/; ListTypeRef{type_ref()}
}

func func_param():Param {
    /name=ident S ":" S t=type_ref/; Param{name, t}
}

func func_params():[]Param {
    /"(" S/;
    params = []Param{};
    (
        params<<func_param();
        (
            /S "," S/;
            params<<func_param()
        )*
    )?;
    /S ")"/;
    params
}

func func_returns():[]TypeRef {
    types=[]TypeRef{};
    (
        /"->" S (types<<type_ref | "(" S (types<<type_ref (S "," S types<<type_ref)*)? S ")")/
    )?;
    types
}

func func_decl():FuncDecl {
    /"fn" end_of_keyword S name=ident S params=func_params S returns=func_returns S "{" S/;
    /body=expr/;
    /S "}"/;
    FuncDecl{name, params, returns, body}
}

func import_decl():ImportDecl {
    /"import" end_of_keyword S/;
    l = loc();
    path = []string{raw_ident()};
    /("." path<<raw_ident)* S ";"/;
    ImportDecl{l, path}
}

func extern_func_decl():ExternFuncDecl {
    /"extern" end_of_keyword S "fn" end_of_keyword S name=ident S params=func_params S returns=func_returns S ";"/;
    ExternFuncDecl{name, params, returns}
}

func field_decl():FieldDecl {
    /name=ident S ":" S t=type_ref S ";"/; FieldDecl{name, t}
}

func struct_decl():StructDecl {
    /"struct" end_of_keyword S name=ident S "{" S/;
    fields=[]FieldDecl{};
    /(fields<<field_decl (S fields<<field_decl)*)? S "}"/;
    StructDecl{name, fields}
}

[export]
func module(module_name:string, path:string):Module {
    decls = []Decl{};
    (
        S();
        decls<<(func_decl()|struct_decl()|import_decl()|extern_func_decl())
    )*;
    S();
    /![^]/;
    Module{module_name, path, decls}
}