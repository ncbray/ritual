extern int_to_rune(i:int):rune
extern runes_to_string(runes:[]rune):string
extern string_to_int(text:string, base:int):int

struct GetName {
    loc:location
    name:Token
}

struct GetAttr {
    loc:location
    expr:Expr
    name:Token
}

struct Call {
    loc:location
    expr:Expr
    args:[]Expr
}

struct BooleanLiteral {
    loc:location
    value:bool
}

struct TupleLiteral {
    loc:location
    args:[]Expr
}

struct FloatLiteral {
    loc:location
    text:string
    postfix:string
}

struct IntLiteral {
    loc:location
    text:string
    base:int
    postfix:string
}

struct StringLiteral {
    loc:location
    value:string
}

struct StringConcat {
    loc:location
    children:[]Expr
}

struct Assign {
    loc:location
    target:Expr
    value:Expr
}

struct Let {
    loc:location
    expr:Expr
}

struct Sequence {
    children:[]Expr
}

struct If {
    loc:location
    cond:Expr
    t:Expr
    f:Expr
}

struct While {
    loc:location
    cond:Expr
    body:Expr
}

struct StructMatch {
    t:TypeRef
}

union Matcher = StructMatch;

struct Case {
    loc:location
    matcher:Matcher
    expr:Expr
}

struct Match {
    loc:location
    cond:Expr
    cases:[]Case
}

struct PrefixOp {
    loc:location
    op:string
    expr:Expr
}

struct BinaryOp {
    loc:location
    left:Expr
    op:string
    right:Expr
}

union Expr = GetName | Call | GetAttr | BooleanLiteral | TupleLiteral | FloatLiteral | IntLiteral | StringLiteral | StringConcat | Assign | Let | Sequence | If | While | Match | PrefixOp | BinaryOp;

struct NamedTypeRef {
    name:Token
}

struct ListTypeRef {
    t:TypeRef
}

// HACK
// TODO: support null.
struct NoTypeRef {
}

union TypeRef = NamedTypeRef | ListTypeRef | NoTypeRef;


struct Param {
    name:Token
    t:TypeRef
}

struct FuncDecl {
    name:Token
    params:[]Param
    returns:[]TypeRef
    body:Expr
}

struct FieldDecl {
    name:Token
    t:TypeRef
}

union MemberDecl = FieldDecl | FuncDecl;

struct StructDecl {
    name:Token
    is_ref:bool
    parent:TypeRef
    members:[]MemberDecl
}

struct ImportDecl {
    loc:location
    path:[]string
}

struct ExternFuncDecl {
    name:Token
    params:[]Param
    returns:[]TypeRef
}

struct TestDecl {
    desc:string
    body:Expr
}

union Decl = FuncDecl | StructDecl | ImportDecl | ExternFuncDecl | TestDecl;

struct Module {
    name:string
    path:string
    decls:[]Decl
}

func S():void {
    /([ \t\n\r]|"//"[^\n]*)*/
}

//func s():void { /[ \t]*/ }
//func EOS():void { /S ";" | s !!"}" | S [^]/ }

func hex_digit():rune {
    /[0-9a-fA-Z]/
}

func number_postfix():string {
    </("i"|"u")("8"|"16"|"32"|"64") | "f"("32"|"64")/>
}

func float_literal():FloatLiteral {
    /l=loc text=<[0-9]+ "." [0-9]+>/; FloatLiteral{l, text, number_postfix()|"f32"}
}

func int_literal():IntLiteral {
    /l=loc "0x" text=<hex_digit+>/; IntLiteral{l, text, 16, number_postfix()|"i32"}
    | /l=loc text=<[0-9]+>/; IntLiteral{l, text, 10, number_postfix()|"i32"}
}

func escape_char():rune {
    /[\\]/;
    ( /[n]/; int_to_rune(0x0A)
    | /[r]/; int_to_rune(0x0D)
    | /[t]/; int_to_rune(0x09)
    | /[0]/; int_to_rune(0x00)
    | /[\\]/; int_to_rune(0x5C)
    | /[x]/; int_to_rune(string_to_int(</hex_digit hex_digit/>, 16))
    | /[u][{] digits=<hex_digit+> [}]/; int_to_rune(string_to_int(digits, 16))
    )
}

func string_value():string {
    /["]/;
    c = []rune{};
    (
        c << /escape_char | [\\]["] | [{][{] | [}][}] | [^\\"{}]/
    )*;
    /["]/;
    runes_to_string(c)
}

func string_fragment():string {
    c = []rune{};
    (
        c << /escape_char | [\\]["] | [{][{] | [}][}] | [^\\"{}]/
    )+;
    runes_to_string(c)
}

func string_literal():Expr {
    l = loc();
    /["]/;
    exprs = []Expr{};
    (
        exprs << StringLiteral{loc(), string_fragment()}
    |
        /[{] exprs << expr [}]/
    )*;
    /["]/;
    StringConcat{l, exprs}
}

func matcher():Matcher {
    t=type_ref(); StructMatch{t}
}

func match_case():Case {
    /l=loc "case" end_of_keyword S m=matcher S ":" S e=expr_or_nothing/; Case{l, m, e}
}

func match_expr():Match {
    /l=loc "match" end_of_keyword S cond=expr S "{"/;
    cases = []Case{};
    /(S cases<<match_case)* S "}"/; Match{l, cond, cases}
}

func if_expr():If {
    /l=loc "if" end_of_keyword S cond=expr S "{" S t=expr_or_nothing S "}"/; (/S "else" S "{" S  f=expr_or_nothing S "}"/| f=Sequence{[]Expr{}}); If{l, cond, t, f}
}

func while_expr():While {
    /l=loc "while" end_of_keyword S cond=expr S "{" S body=expr_or_nothing S "}"/; While{l, cond, body}
}

func expr_atom():Expr {
    (/l=loc "(" S/;
        (e=expr();
            (args=[]Expr{e};
                /(S "," S args<<expr)+/;
                /S ")"/;
                TupleLiteral{l, args}
            | /S ")"/; e
            )
        | /S ")"/; TupleLiteral{l, []Expr{}}
        )
    | float_literal()
    | int_literal()
    | string_literal()
    | l=loc();
        ( /"true" end_of_keyword/; BooleanLiteral{l, true}
        | /"false" end_of_keyword/; BooleanLiteral{l, false}
        )
    | /l=loc "let" end_of_keyword S/; Let{l, expr_postfix()}
    | if_expr()
    | while_expr()
    | match_expr()
    | GetName{loc(), ident()}
    )
}

func expr_postfix():Expr {
    e=expr_atom();
    ( /S l=loc "." S/; e=GetAttr{l, e, ident()}
    | /S l=loc "(" S/; args=[]Expr{}; /(args<<expr (S "," S args<<expr)*)? S ")"/; e=Call{l, e, args}
    )*;
    e
}

func expr_prefix():Expr {
  /l=loc op=<"+"|"-"|"!"> S/; PrefixOp{l, op, expr_prefix()}
  | expr_postfix()
}

func expr_mul():Expr {
    e=expr_prefix();
    (
        /S l=loc op=<"*"|"/"|"%"> S other=expr_prefix/;
        e = BinaryOp{l, e, op, other}
    )*;
    e
}

func expr_add():Expr {
    e=expr_mul();
    (
        /S l=loc op=<"+"|"-"> S other=expr_mul/;
        e = BinaryOp{l, e, op, other}
    )*;
    e
}

func expr_compare():Expr {
    e=expr_add();
    (
        /S l=loc op=<"<="|"<"|">="|">"|"!="|"=="> S other=expr_add/;
        e = BinaryOp{l, e, op, other}
    )?;
    e
}

func expr_assign():Expr {
    e=expr_compare();
    (
        /S l=loc "=" S/;
        value=expr_compare();
        e=Assign{l, e, value}
    )?;
    e
}

func expr_sequence():Expr {
    e=expr_assign();
    (
        exprs=[]Expr{e};
        (
            /S ";" S/;
            exprs<<expr_assign()
        )+;
        e=Sequence{exprs}
    )?;
    e
}

func expr():Expr {
    expr_sequence()
}

func expr_or_nothing():Expr {
    /e=expr_sequence (S ";")?/; e
    | Sequence{[]Expr{}}
}

func type_ref():TypeRef {
    NamedTypeRef{ident()}
    | /"[]" S/; ListTypeRef{type_ref()}
}

func func_param():Param {
    /name=ident S ":" S t=type_ref/; Param{name, t}
}

func func_params():[]Param {
    /"(" S/;
    params = []Param{};
    (
        params<<func_param();
        (
            /S "," S/;
            params<<func_param()
        )*
    )?;
    /S ")"/;
    params
}

func func_returns():[]TypeRef {
    types=[]TypeRef{};
    (
        /"->" S (types<<type_ref | "(" S (types<<type_ref (S "," S types<<type_ref)*)? S ")")/
    )?;
    types
}

func func_decl():FuncDecl {
    /"fn" end_of_keyword S name=ident S params=func_params S returns=func_returns S "{" S body=expr_or_nothing S "}"/;
    FuncDecl{name, params, returns, body}
}

func import_decl():ImportDecl {
    /"import" end_of_keyword S/;
    l = loc();
    path = []string{raw_ident()};
    /("." path<<raw_ident)* S ";"/;
    ImportDecl{l, path}
}

func extern_func_decl():ExternFuncDecl {
    /"extern" end_of_keyword S "fn" end_of_keyword S name=ident S params=func_params S returns=func_returns S ";"/;
    ExternFuncDecl{name, params, returns}
}

func field_decl():FieldDecl {
    /name=ident S ":" S t=type_ref S ";"/; FieldDecl{name, t}
}

func member_decl():MemberDecl {
    field_decl() | func_decl()
}

func struct_decl():StructDecl {
    is_ref=(/"struct"/; false | /"class"/; true);
    /end_of_keyword S name=ident S/;
    (/":" S parent=type_ref S/ | parent=NoTypeRef{});
    /"{" S/;
    members=[]MemberDecl{};
    /(members<<member_decl (S members<<member_decl)*)? S "}"/;
    StructDecl{name, is_ref, parent, members}
}

func test_decl():TestDecl {
    /"test" S desc=string_value S "{" S body=expr_or_nothing S "}"/;
    TestDecl{desc, body}
}

[export]
func module(module_name:string, path:string):Module {
    decls = []Decl{};
    (
        S();
        decls<<(func_decl()|struct_decl()|import_decl()|extern_func_decl()|test_decl())
    )*;
    S();
    /![^]/;
    Module{module_name, path, decls}
}